import org.bau.Math
import org.bau.Env

fun main()
    n := 4
    if Env.argCount() > 1
        n = Math.parseInt(Env.arg(1))
    println('Pfannkuchen(' n ') is ' fannkuch(n))
    
fun fannkuch(n int) int
    perm1 : int[n]
    for i := until(n)
        perm1[i] = i
    perm : int[n]
    count : int[n]
    f := 0
    flips := 0
    nperm := 0
    checksum := 0
    k := 0
    r := n
    while r > 0
        while r > 1 
            count[r - 1] = r
            r -= 1
        for i := until(n)
            perm[i] = perm1[i]
        # Count flips and update max and checksum
        f = 0
        k = perm[0]
        while k <> 0
            for i := until(perm.len)
                break 2 * i >= k
                t := perm[i]
                perm[i] = perm[k - i]
                perm[k - i] = t
            k = perm[0]
            f += 1
        if f > flips
            flips = f
        if (nperm & 1) = 0
            checksum += f
        else 
            checksum -= f
        # Use incremental change to generate another permutation
        more := 1
        while more
            if r = n
                println(checksum)
                return flips
            p0 := perm1[0]
            i := 0
            while i < r
                j := i + 1
                perm1[i] = perm1[j]
                i = j
            perm1[r] = p0
            count[r] -= 1
            if count[r] > 0
                more = 0
            else 
                r += 1
        nperm += 1
    return flips
