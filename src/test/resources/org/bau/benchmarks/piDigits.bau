# The Computer Language Benchmarks Game
# https://salsa.debian.org/benchmarksgame-team/benchmarksgame/

import org.bau.BigInt
    newBigInt
import org.bau.Math
import org.bau.Env

fun main()
    n := 10_000
    if Env.argCount() > 1
        n = Math.parseInt(Env.arg(1))
    k, i := 0
    buff : i8[10]
    while i < n
        k += 1
        nextTerm(k)
        continue num.compareTo(acc) > 0
        d : extractDigit(3)
        continue d <> extractDigit(4)
        eliminateDigit(d)
        buff[i % 10] = ord('0') + d
        i += 1
        if i % 10 = 0
            println(buff ' : ' i)

acc := newBigInt(0)
den := newBigInt(1)
num := newBigInt(1)

fun extractDigit(nth int) int
    return num.multiplyInt(nth).add(acc).divide(den).intValue()

fun eliminateDigit(d int)
    acc = acc.subtract(den.multiplyInt(d))
    acc = acc.multiplyInt(10)
    num = num.multiplyInt(10)

fun nextTerm(k int)
    acc = acc.add(num.shiftLeft(1))
    acc = acc.multiplyInt(k * 2 + 1)
    den = den.multiplyInt(k * 2 + 1)
    num = num.multiplyInt(k)
